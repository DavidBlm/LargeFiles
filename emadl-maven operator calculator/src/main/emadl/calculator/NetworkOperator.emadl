/* (c) https://github.com/MontiCore/monticore */
/* Limited to 5 for the 5 possible operators
0: Add, 1: Substract, 2: Multiply, 3: Divide, 4: Modulo

*/
package calculator;

component NetworkOperator{
    ports in Z(0:255)^{1, 28, 28} data,
         out Q{0:1}^{5} result;

    implementation CNN {
        def conv(channels, kernel=1, stride=1){
            Convolution(kernel=(kernel,kernel),channels=channels) ->
            Relu() ->
            Pooling(pool_type="max", kernel=(2,2), stride=(stride,stride))
        }

        data ->
        conv(kernel=5, channels=20, stride=2) ->
        conv(kernel=5, channels=50, stride=2) ->
        FullyConnected(units=500) ->
        Relu() ->
        Dropout() ->
        FullyConnected(units=5) ->
        Softmax() ->
        result;
    }
}
